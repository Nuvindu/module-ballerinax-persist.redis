// Copyright (c) 2023 WSO2 LLC. (http://www.wso2.org) All Rights Reserved.
//
// WSO2 LLC. licenses this file to you under the Apache License,
// Version 2.0 (the "License"); you may not use this file except
// in compliance with the License.
// You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied.  See the License for the
// specific language governing permissions and limitations
// under the License.

import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        maven {
            url = 'https://maven.pkg.github.com/ballerina-platform/plugin-gradle'
            credentials {
                username System.getenv('packageUser')
                password System.getenv('packagePAT')
            }
        }
    }
    dependencies {
        classpath "io.ballerina:plugin-gradle:${project.ballerinaGradlePluginVersion}"
    }
}

description = 'Ballerina - Persist Ballerina Generator'

def packageName = 'persist.redis'
def packageOrg = 'ballerinax'
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")

def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/Ballerina.toml")
def ballerinaTomlFile = new File("$project.projectDir/Ballerina.toml")

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-')
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", '')
    }
}

apply plugin: 'io.ballerina.plugin'

ballerina {
    packageOrganization = packageOrg
    module = packageName
    langVersion = ballerinaLangVersion
    testCoverageParam = "--code-coverage --coverage-format=xml --includes=*"
}

dependencies {
    externalJars(group: 'io.ballerina.stdlib', name: 'persist-native', version: "${stdlibPersistVersion}") {
        transitive = false
    }
}

task updateTomlFiles {
    doLast {
        def stdlibDependentPersistVersion = stripBallerinaExtensionVersion(project.stdlibPersistVersion)

        def newConfig = ballerinaTomlFilePlaceHolder.text.replace('@project.version@', project.version.toString())
        newConfig = newConfig.replace('@toml.version@', tomlVersion)
        newConfig = newConfig.replace('@persist.version@', stdlibDependentPersistVersion)
        newConfig = newConfig.replace('@persist.native.version@', project.stdlibPersistVersion)
        ballerinaTomlFile.text = newConfig
    }
}

task commitTomlFiles {
    doLast {
        project.exec {
            ignoreExitValue true
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "git commit -m \"[Automated] Update native jar versions in toml files\" Ballerina.toml Dependencies.toml"
            } else {
                commandLine 'sh', '-c', "git commit -m \"[Automated] Update native jar versions in toml files\" Ballerina.toml Dependencies.toml"
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact source: createArtifactZip, extension: 'zip'
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri("https://maven.pkg.github.com/ballerina-platform/module-${packageOrg}-${packageName}")
            credentials {
                username = System.getenv('publishUser')
                password = System.getenv('publishPAT')
            }
        }
    }
}

static def checkExecResult(executionResult, failText, standardOutput) {
    if (executionResult != null) {
        Provider<ExecResult> execResultProvider = executionResult.getProvider()
        int exitCode = execResultProvider.get().getExitValue()
        if (exitCode != 0) {
            throw new GradleException('Non-zero exit value: ' + exitCode)
        }
        if (standardOutput.toString().contains(failText)) {
            throw new GradleException('"' + failText + '" string in output: ' + standardOutput.toString())
        }
    } else {
        throw new GradleException('Returned a null execResult object')
    }
}

task createRedisTestDockerImage(type: Exec) {
    if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
        def standardOutput = new ByteArrayOutputStream()
        commandLine 'sh', '-c', "docker build -f ${project.projectDir}/tests/resources/Dockerfile -t ballerina-persist-redis" +
                " -q ${project.projectDir}/tests/resources"
        doLast {
            checkExecResult(executionResult, 'Error', standardOutput)
            sleep(10 * 1000)
        }
    } else {
        def standardOutput = new ByteArrayOutputStream()
        commandLine 'cmd', '/c', "docker build -f ${project.projectDir}/tests/resources/Dockerfile -t ballerina-persist-redis" +
                " -q ${project.projectDir}/tests/resources"
        doLast {
            checkExecResult(executionResult, 'Error', standardOutput)
            sleep(10 * 1000)
        }
    }
}

def checkRedisTestDockerContainerStatus(containerName) {
    if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
        try {
            return exec {
                commandLine 'sh', '-c', "docker exec ${containerName} redis-cli"
            }.exitValue
        } catch (all) {
            return 1;
        }
    } else {
        try {
            return exec {
                commandLine 'cmd', '/c', "docker exec ${containerName} redis-cli"
            }.exitValue
        } catch (all) {
            return 1;
        }
    }
}

task startRedisTestDockerContainer(type: Exec) {
    if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
        def standardOutput = new ByteArrayOutputStream()
        commandLine 'sh', '-c',
                "docker run --rm -d --name ballerina-persist-redis -p 6379:6379 -d ballerina-persist-redis"
        def healthCheck = 1;
        def counter = 0;
        doLast {
            checkExecResult(executionResult, 'Error', standardOutput)
            while (healthCheck != 0 && counter < 12) {
                sleep(5 * 1000)
                healthCheck = checkRedisTestDockerContainerStatus("ballerina-persist-redis")
                counter = counter + 1;
            }
            if (healthCheck != 0) {
                throw new GradleException("Docker container 'ballerina-persist-redis' health test exceeded timeout!")
            }
        }
    } else {
        def standardOutput = new ByteArrayOutputStream()
        commandLine 'cmd', '/c',
                "docker run --rm -d --name ballerina-persist-redis -p 6379:6379 -d ballerina-persist-redis"
        def healthCheck = 1;
        def counter = 0;
        doLast {
            checkExecResult(executionResult, 'Error', standardOutput)
            while (healthCheck != 0 && counter < 12) {
                sleep(5 * 1000)
                healthCheck = checkRedisTestDockerContainerStatus("ballerina-persist-redis")
                counter = counter + 1;
            }
            if (healthCheck != 0) {
                throw new GradleException("Docker container 'ballerina-persist-redis' health test exceeded timeout!")
            }
        }
    }
}

task stopRedisTestDockerContainer() {
    doLast {
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            try {
                def stdOut = new ByteArrayOutputStream()
                exec {
                    commandLine 'sh', '-c', "docker stop ballerina-persist-redis"
                    standardOutput = stdOut
                }
            } catch (all) {
                println("Process can safely ignore stopRedisTestDockerContainer task")
            }
        } else {
            try {
                def stdOut = new ByteArrayOutputStream()
                exec {
                    commandLine 'cmd', '/c', "docker stop ballerina-persist-redis"
                    standardOutput = stdOut
                }
            } catch (all) {
                println("Process can safely ignore stopRedisTestDockerContainer task")
            }
        }
    }
}

updateTomlFiles.dependsOn copyStdlibs
// startRedisTestDockerContainer.dependsOn createRedisTestDockerImage

// build.dependsOn "generatePomFileForMavenPublication"
build.dependsOn ":${packageName}-native:build"
// build.finalizedBy stopRedisTestDockerContainer

test.dependsOn ":${packageName}-native:build"
// test.dependsOn startRedisTestDockerContainer
